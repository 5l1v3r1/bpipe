apply plugin: 'eclipse'
apply plugin: 'groovy'

VERSION="0.9.7"
STAGE="build/stage/bpipe-$VERSION"

repositories {
    flatDir(dirs: file('local-lib'))
    mavenCentral()
    maven { url "http://www.gridgainsystems.com/maven2" }
}

dependencies {
    groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.8.2'
    compile files(fileTree(dir:'local-lib', includes:['*.jar']))
    compile ('org.gridgain:gridgain:4.0.2c') { transitive = false }
    compile ('com.hazelcast:hazelcast-all:2.1.2') { transitive = false }
    testCompile group: 'junit', name: 'junit', version: '4.8.2'
}

task stage(dependsOn: jar) << {
  ant.mkdir(dir: STAGE)
  ant.mkdir(dir: "$STAGE/bin")
  ant.mkdir(dir: "$STAGE/lib")
  ant.copy(todir: "$STAGE/bin") {
    ant.fileset(dir: 'bin', includes: "**")
  }
  new File(STAGE+'/bin/bpipe').text = 
          new File("bin/bpipe").text.replaceAll("VERSION=0.0.0","VERSION=$VERSION").replaceAll("BUILDDATE=0","BUILDDATE="+String.valueOf(System.currentTimeMillis()))

  ant.copy(todir: "$STAGE/lib") {
    // do not distribute mail.jar due to license
    ant.fileset(dir: 'local-lib', includes: "*.jar", excludes: "mail.jar,gridgain-*.jar,hazelcast-*.jar")
    ant.fileset(dir: 'build/libs', includes: "bpipe.jar")
  }
  ant.mkdir(dir: "$STAGE/html")
  ant.copy(todir: "$STAGE/html") {
    ant.fileset(dir: 'src/main/html/bpipe', includes: "*")
  }
  ant.copy(todir: "$STAGE") {
    ant.fileset(dir: 'src/main/config', includes: "bpipe.config")
  }
}

task dist(dependsOn: stage) << {
  ant.tar(destfile: "build/bpipe-${VERSION}.tar") {
    ant.fileset(dir: "build/stage", includes: "bpipe-${VERSION}/lib/**", excludes:"**/*.swp")
    ant.fileset(dir: "build/stage", includes: "bpipe-${VERSION}/bpipe.config", excludes:"**/*.swp")
    ant.fileset(dir: "build/stage", includes: "bpipe-${VERSION}/html/**", excludes:"**/*.swp")
    ant.tarfileset(dir: "build/stage", filemode:"755", includes: "bpipe-${VERSION}/bin/**", excludes:"**/*.swp")
  }
  ant.gzip(destfile:"build/bpipe-${VERSION}.tar.gz", src: "build/bpipe-${VERSION}.tar")
}
